plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/upgradedshulkers.accesswidener")
}

repositories {
	maven { url 'https://maven.fabricmc.net' }
	maven { url "https://maven.kyrptonaught.dev" }
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases" }

	// Shulker Box Tooltip
	maven { url "https://maven.misterpemodder.com/libs-release" }
	// Mod Menu (Shulker Box Tooltip deps)
	maven { url "https://raw.githubusercontent.com/TerraformersMC/Archive/main/releases" }
	// Cloth Config (Shulker Box Tooltip deps)
	maven { url "https://maven.shedaniel.me" }
	// Fabric ASM (Shulker Box Tooltip deps)
	maven { url "https://jitpack.io" }
}
dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	//modImplementation group: 'net.devtech', name: 'arrp', version: '0.3.4'
	//include group: 'net.devtech', name: 'arrp', version: '0.3.4'

	//shulkerutils
	modImplementation 'net.kyrptonaught:shulkerutils:1.0.4-1.18'
	include 'net.kyrptonaught:shulkerutils:1.0.4-1.18'

	modImplementation 'net.kyrptonaught:kyrptconfig:1.4.6-1.18'
	include 'net.kyrptonaught:kyrptconfig:1.4.6-1.18'

	//Quickshulker
	optionalDependency('net.kyrptonaught:quickshulker:1.3.5-1.18')

	optionalDependency ("me.shedaniel:RoughlyEnoughItems-api-fabric:8.2.479")
	optionalDependency ("me.shedaniel:RoughlyEnoughItems-fabric:8.2.479")
	optionalDependency ("me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:8.2.479")

	optionalDependency ("com.misterpemodder:shulkerboxtooltip:3.0.9+1.18.2")
}
//thanks techreborn
def optionalDependency(String dep) {
	dependencies.modRuntimeOnly (dep) {
		exclude group: "net.fabricmc.fabric-api"
		exclude module: "nbt-crafting"
	}
	dependencies.modCompileOnly (dep) {
		exclude group: "net.fabricmc.fabric-api"
		exclude module: "nbt-crafting"
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
}
